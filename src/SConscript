Import('use');

def isOSX():
    import re
    import sys
    return "darwin" in sys.platform

def isWindows():
    import re
    import sys
    return "win32" in sys.platform

obj_files = []

usePartialLinking = False

def setupPartialLinking():
    def linkPartial(target, source, env):
        # FIXME: replace 'ld' with a more generic thing like $PARTIAL_LINK which should
        # be set up by each platform
        env.Execute(Action('@ld -r -o %s %s' % (target[0].path, ' '.join([x.path for x in source]))))

    linkLine = 'Partial link $TARGET $SOURCES'
    try:
        linkLine = use['LINKCOMSTR']
    except KeyError:
        pass

    import SCons
    static_obj, shared_obj = SCons.Tool.createObjBuilders(use)
    partialBuilder = Builder(action = Action(linkPartial, cmdstr = linkLine),
                             suffix = '.o',
                             src_suffix = '.o',
                             src_builder = static_obj)
    use.Append(BUILDERS = {'PartialObject' : partialBuilder})

if usePartialLinking:
    setupPartialLinking()

partial = [0]
def addFiles(files):
    if usePartialLinking:
        obj_files.append(use.PartialObject('partial%d.o' % partial[0], map(lambda x: x.replace('src/', ''), files)))
        partial[0] = partial[0] + 1
    else:
        obj_files.append(map(lambda x: x.replace('src/', ''), files))

def findDirectory(name):
    import os.path
    where = '.'
    # just try 5 directories
    for i in xrange(0, 5):
        if os.path.exists("%s/%s" % (where, name)):
            return "%s/%s" % (where, name)
        where = os.path.join(where, '..')
    raise Exception("Could not find the %s directory" % name)

import sys
sys.path.append(findDirectory("scons"))
import helpers

obj_files = []

def findCmake():
    return findDirectory('cmake')
    
filelist = helpers.read_cmake_list("%s/FileList.cmake" % findCmake())

def sprigLibrary(env):
    sprig = env.Clone()
    source = Split("""
util/sdl/sprig/SPG_extended.c
util/sdl/sprig/SPG_misc.c
util/sdl/sprig/SPG_polygon.c
util/sdl/sprig/SPG_primitives.c
util/sdl/sprig/SPG_rotation.c
util/sdl/sprig/SPG_surface.c
""")

    return sprig.StaticLibrary('sprig', source)

def stretchLibrary(env):
    stretch = env.Clone()
    # FIXME: asm has some bug in it, try to fix it later
    stretch.Append(CPPDEFINES = ['SDL_STRETCH_DISABLE_ASM'])
    source = Split("""
util/sdl/stretch/sdlstretch.c
util/sdl/stretch/sdlstretchcode.c
""")

    return stretch.StaticLibrary('stretch', source)

def alGifLibrary(env):
    gif = env.Clone()
    source = Split("""
util/allegro/gif/algif.c
util/allegro/gif/gif.c
util/allegro/gif/lzw.c
""")
    return gif.StaticLibrary('algif', source)

def sdlImageLibrary(env):
    image = env.Clone()
    image.Append(CPPDEFINES = ['LOAD_PNG', 'LOAD_PCX', 'LOAD_GIF'])
    source = Split("""
util/sdl/image/IMG.c
util/sdl/image/IMG_jpg.c
util/sdl/image/IMG_pnm.c
util/sdl/image/IMG_xpm.c
util/sdl/image/IMG_ImageIO.c
util/sdl/image/IMG_lbm.c
util/sdl/image/IMG_tga.c
util/sdl/image/IMG_xv.c
util/sdl/image/IMG_bmp.c
util/sdl/image/IMG_pcx.c
util/sdl/image/IMG_tif.c
util/sdl/image/IMG_xxx.c
util/sdl/image/IMG_gif.c
util/sdl/image/IMG_png.c
util/sdl/image/IMG_savepng.c
util/sdl/image/IMG_xcf.c
""")

    return image.StaticLibrary('image',source)

def sdlMixerLibrary(env):
    use = env
    return SConscript('util/sdl/mixer/SConscript', exports = ['use'])

def sflLibrary(env):
    use = env
    return SConscript('util/sfl/SConscript', exports = ['use'])

def soxLibrary(env):
    use = env
    return SConscript('util/sox/SConscript', exports = ['use'])

def gmeLibrary(env):
    use = env
    return SConscript('util/gme/SConscript', exports = ['use'])

#for i in filelist:
#    # print "%s = %s" % (i, filelist[i])
#    # meh, kind of a hack
#    if i == 'SCRIPT_SRC':
#        # call the python and ruby checks here
#        env = use.Clone()
#        # config = env.Configure()
#        # Ugly way to pass the custom tests in
#        config = env.Configure(custom_tests = env['PAINTOWN_TESTS'])
#        # config.CheckRuby()
#        config.CheckPython()
#        env = config.Finish()
#        use.Append(LIBS = env['LIBS'])
#        use.Replace(LIBS = unique(use['LIBS']))
#        files = filelist[i]
#        # abstract this so we don't duplicate code
#        obj_files.append(map(lambda x: env.StaticObject(x.replace('src/', '')), files))
#    else:
#        addFiles(filelist[i])

for i in filelist:
    addFiles(filelist[i])

# FIXME: somehow get this information from the scons env
def useSDL():
    return use['PAINTOWN_BACKEND'] == 'sdl'

def useAllegro5():
    return use['PAINTOWN_BACKEND'] == 'allegro5'

def useAndroid():
    import os
    try:
        return int(os.environ['android'])
    except KeyError:
        return False

def useMinpspw():
    import os
    try:
        return int(os.environ['minpspw'])
    except KeyError:
        return False

def useWii():
    import os
    try:
        return int(os.environ['wii'])
    except KeyError:
        return False

def icon():
    if isWindows() and not useMinpspw() and not useWii():
        return ['util/windows/game.res']
    else:
        return []

def pcreLibrary(env):
    pcreEnv = env.Clone()
    env = pcreEnv
    return use.SConscript('util/pcre/SConstruct', exports = ['env'])

def dumbLibrary(env):
    dumbEnv = env.Clone()
    env = dumbEnv
    return use.SConscript('util/dumb/SConscript', exports = ['env'])

def hawknlLibrary(env):
    hawkEnv = env.Clone()
    env = hawkEnv
    # if isOSX():
    #    env.Append(CPPDEFINES = 'MACOSX')
    return env.SConscript('util/network/hawknl/SConscript', exports = ['env'])

def minizipLibrary(env):
    return env.SConscript('util/zip/SConscript', exports = ['env'])

def utilLibrary(env):
    utilEnv = env.Clone()
    env = utilEnv
    return env.SConscript('util/SConscript', exports = ['env'])

def main():
    return ['util/xmain.cpp']
    
def androidSDLMain():
    if useAndroid():
        return ['util/android/jni/SDL_android_main.cpp']
    else:
        return []

def psp_prx(env):
    if env['PAINTOWN_USE_PRX']:
        # FIXME: Use the pspsdk path from the top level SConstruct
        return ['/opt/pspsdk/psp/sdk/lib/prxexports.o']
    else:
        return []

use.Depends(use.Peg('openbor/data.peg'), 'mugen/parser/peg.py')

util = utilLibrary(use)

# There is a mutual dependancy between the util library and sdl/mixer so a hack
# is to include the util library twice, once here
use.Prepend(LIBS = [util])

use.Prepend(LIBS = [dumbLibrary(use)])
if use['PAINTOWN_NETWORKING']:
    use.Prepend(LIBS = [hawknlLibrary(use)])

use.Prepend(LIBS = [gmeLibrary(use), sflLibrary(use), pcreLibrary(use), minizipLibrary(use)])
if not useSDL() and not useAllegro5():
    use.Prepend(LIBS = [alGifLibrary(use)])
if useSDL() and not useMinpspw():
    use.Prepend(LIBS = [sdlImageLibrary(use), sprigLibrary(use), sdlMixerLibrary(use), stretchLibrary(use)])
elif useMinpspw():
    use.Prepend(LIBS = [sprigLibrary(use), stretchLibrary(use)])

# This is the second time the util library is linked in, noted above
#use.Prepend(LIBS = [util])
#use.Append(LIBS = [util])

def addObjects(objects):
    obj_files.extend(objects)

SConscript('test/SConstruct', exports = ['use'])

# Make this a function so we don't clobber the 'use' variable
def stuff(use):
    SConscript('mugen/SConscript', exports = ['use'])
    SConscript('paintown-engine/SConscript', exports = ['use'])

paintownEnv = use.Clone()
stuff(paintownEnv)

x = []
# FIXME: adding psp_prx here is ugly, find a better way
x.append(paintownEnv.Program('paintown', psp_prx(use) + obj_files + main() + icon() + androidSDLMain()))

Return('x')
